start: instruction+

instruction: assignment
           | conditional
           | loop
           | try_catch_stmt
           | throw_stmt
           | exec_stmt
           | flow_control ";"
           | print_stmt
           | block

block: "{" instruction+ "}"

// ---------------------
// ASSIGNMENT
// ---------------------
assignment: assign_expr ";"

assign_expr: VAR "=" expression

// ---------------------
// CONDITIONAL
// ---------------------
conditional: "if" "(" expression ")" block ("else" block)?

// ---------------------
// LOOPS
// ---------------------
loop: while_loop
    | do_while_loop
    | for_loop

while_loop: "while" "(" expression ")" block

do_while_loop: "do" block "while" "(" expression ")" ";"

for_loop: "for" "(" assign_expr ";" expression ";" assign_expr ")" block

// ---------------------
// FLOW CONTROL
// ---------------------
flow_control: "break"              -> break_stmt
            | "continue"           -> continue_stmt
            | "return" expression? -> return_stmt

// ---------------------
// EXCEPTIONS
// ---------------------
try_catch_stmt: "try" block "catch" "(" VAR ")" block
              | "try" block "catch" block

throw_stmt: "throw" expression ";"

exec_stmt: "exec" "(" expression ")" ";"
print_stmt: "print" "(" expression ")" ";"

// ---------------------
// EXPRESSIONS
// ---------------------
?expression: logical_or

logical_or: logical_and ("or" logical_and)*
logical_and: comparison ("and" comparison)*

comparison: sum (COMP_OP sum)*

sum: product ((PLUS | MINUS) product)*
product: atom ((MULT | DIV) atom)*

atom: NUMBER           -> number
    | STRING           -> string
    | TRUE             -> true
    | FALSE            -> false
    | VAR              -> var_access
    | list             -> list_structure
    | dict             -> dict_structure
    | "(" expression ")"

// ---------------------
// LIST / DICT
// ---------------------
list: "[" [expression ("," expression)*] "]"

dict: "{" [pair ("," pair)*] "}"
pair: expression ":" expression

// ---------------------
// TOKENS
// ---------------------
PLUS: "+"
MINUS: "-"
MULT: "*"
DIV: "/"
COMP_OP: "==" | "!=" | "<=" | ">=" | "<" | ">"

TRUE: "true"
FALSE: "false"

VAR: /[a-zA-Z_][a-zA-Z0-9_]*/

// ---------------------
// IMPORTS
// ---------------------
%import common.SIGNED_NUMBER -> NUMBER
%import common.ESCAPED_STRING -> STRING
%import common.WS
COMMENT: /\/\/.*/
%ignore WS
%ignore COMMENT
